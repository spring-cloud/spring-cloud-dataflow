buildscript {
	ext {
		springBootVersion = '2.0.0.RELEASE'
		springCloudSkipper100VersionTag = '1.0.0'
		springCloudSkipper100Version = "${springCloudSkipper100VersionTag}.RELEASE"
		springCloudSkipper101VersionTag = '1.0.1'
		springCloudSkipper101Version = "${springCloudSkipper101VersionTag}.RELEASE"		
		springCloudSkipper102VersionTag = '1.0.2'
		springCloudSkipper102Version = "${springCloudSkipper102VersionTag}.RELEASE"
		springCloudSkipper103VersionTag = '1.0.3'
		springCloudSkipper103Version = "${springCloudSkipper103VersionTag}.RELEASE"
		springCloudSkipper104VersionTag = '1.0.4'
		springCloudSkipper104Version = "${springCloudSkipper104VersionTag}.RELEASE"
		springCloudSkipper105VersionTag = '1.0.5'
		springCloudSkipper105Version = "${springCloudSkipper105VersionTag}.RELEASE"
		springCloudSkipper110VersionTag = '1.1.0'
		springCloudSkipper110Version = "${springCloudSkipper110VersionTag}.RELEASE"
		// keep 11x as a latest snapshot version from 1.1.x branch
		springCloudSkipper11xVersionTag = '1.1.3'
		springCloudSkipper11xVersion = "${springCloudSkipper11xVersionTag}.BUILD-SNAPSHOT"
		springCloudSkipper20xVersionTag = '2.0.0'
		springCloudSkipper20xVersion = "${springCloudSkipper20xVersionTag}.BUILD-SNAPSHOT"
		junitPlatformVersion = '1.3.2'
		junitJupiterVersion = '5.3.2'
		dockerComposeRuleJunit4Version = '0.33.0'
		gradleDockerVersion = '0.17.2'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://dl.bintray.com/palantir/releases" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("com.palantir.gradle.docker:gradle-docker:${gradleDockerVersion}")
	}
}

def getResolvedVersionOf(configuration, dependency) {
	return configuration.resolvedConfiguration.firstLevelModuleDependencies.findAll{
	it.moduleName == dependency
	}.moduleArtifacts.first()
}

def driversProjects() {
	subprojects.findAll { project ->
		project.name.contains('spring-cloud-skipper-acceptance-image-drivers')
	}
}

def legacyProjects() {
	subprojects.findAll { project ->
		project.name.contains('spring-cloud-skipper-acceptance-image-legacy')
	}
}

configure(allprojects) {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'io.spring.dependency-management'

	group = 'org.springframework.cloud.skipper.acceptance'

	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://repo.spring.io/release" }
		maven { url "https://dl.bintray.com/palantir/releases" }
		mavenLocal()
		maven {
			url "https://repo.springsource.org/ext-private-local"
			credentials {
				username = System.getenv('ARTIFACTORY_USERNAME') ?: artifactoryUsername
				password = System.getenv('ARTIFACTORY_PASSWORD') ?: artifactoryPassword
			}
		}
	}

	compileJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	compileTestJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	dependencyManagement {
		imports {
		}
		dependencies {
			dependency "com.palantir.docker.compose:docker-compose-rule-junit4:$dockerComposeRuleJunit4Version"
			dependency "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"
			// to run junit5 in sts
			dependency "org.junit.platform:junit-platform-engine:$junitPlatformVersion"
			dependency "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
			dependency "org.junit.platform:junit-platform-runner:$junitPlatformVersion"
			dependency "org.junit.platform:junit-platform-suite-api:$junitPlatformVersion"
		}
	}
}

configure(subprojects) { subproject ->
	apply plugin: 'org.springframework.boot'

	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}
	
	test {
		testLogging {
			events 'STARTED', 'PASSED', 'FAILED', 'SKIPPED'
		}
		useJUnitPlatform {
			if (project.hasProperty('skipperIncludeTags') && skipperIncludeTags.size() > 0) {
				includeTags = skipperIncludeTags.split(',')
			}
			if (project.hasProperty('skipperExcludeTags') && skipperExcludeTags.size() > 0) {
				excludeTags = skipperExcludeTags.split(',')
			}
		}
	}
}

configure(driversProjects()) { subproject ->
	def prefix = subproject.name.tokenize('-').last()
	def val = rootProject.ext["springCloudSkipper" + prefix + "Version"]
	apply plugin: 'com.palantir.docker'

	dependencies {
		compile("org.springframework.cloud.skipper.acceptance.app:skipper-server-with-drivers${prefix}:${val}")
	}

	docker {
		name "spring-cloud-skipper-acceptance-image-drivers${prefix}"
		tags "${val}"
		dockerfile file('../spring-cloud-skipper-acceptance-image-template1/src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.compile, "skipper-server-with-drivers${prefix}").file.absolutePath[0], '../spring-cloud-skipper-acceptance-image-template1/src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.compile, "skipper-server-with-drivers${prefix}").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	build.dependsOn 'dockerTag'
}

configure(legacyProjects()) { subproject ->
	def prefix = subproject.name.tokenize('-').last()
	def val = rootProject.ext["springCloudSkipper" + prefix + "Version"]
	apply plugin: 'com.palantir.docker'

	dependencies {
		compile("org.springframework.cloud:spring-cloud-skipper-server:${val}")
	}

	docker {
		name "spring-cloud-skipper-acceptance-image-legacy${prefix}"
		tags "${val}"
		dockerfile file('../spring-cloud-skipper-acceptance-image-template1/src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.compile, "spring-cloud-skipper-server").file.absolutePath[0], '../spring-cloud-skipper-acceptance-image-template1/src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.compile, "spring-cloud-skipper-server").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	build.dependsOn 'dockerTag'
}

project('spring-cloud-skipper-acceptance-core') {
	dependencies {
		compile('com.palantir.docker.compose:docker-compose-rule-junit4')
		compile('org.junit.jupiter:junit-jupiter-api')
		compile('org.junit.jupiter:junit-jupiter-engine')
		compile('org.junit.jupiter:junit-jupiter-params')

		compile('org.junit.platform:junit-platform-engine')
		compile('org.junit.platform:junit-platform-launcher')
		compile('org.junit.platform:junit-platform-runner')
		compile('org.junit.platform:junit-platform-suite-api')

		testCompile('org.springframework.boot:spring-boot-starter-test')
	}
}

project('spring-cloud-skipper-acceptance-tests') {
	dependencies {
		compile project(":spring-cloud-skipper-acceptance-core")
		testCompile('org.springframework.boot:spring-boot-starter-test')
		testCompile('org.springframework.boot:spring-boot-starter-web')
	}

	rootProject.subprojects.findAll {
		it.name.contains('spring-cloud-skipper-acceptance-image')
	}.collect {
		build.dependsOn ":${it.name}:docker"
	}
}

buildscript {
	ext {
		springBootVersion = '2.0.0.RELEASE'
		springCloudSkipper100VersionTag = '1.0.0'
		springCloudSkipper100Version = "${springCloudSkipper100VersionTag}.RELEASE"
		springCloudSkipper101VersionTag = '1.0.1'
		springCloudSkipper101Version = "${springCloudSkipper101VersionTag}.RELEASE"
		springCloudSkipper102VersionTag = '1.0.2'
		springCloudSkipper102Version = "${springCloudSkipper102VersionTag}.BUILD-SNAPSHOT"
		junitPlatformVersion = '1.1.0'
		junitJupiterVersion = '5.1.0'
		dockerComposeRuleJunit4Version = '0.33.0'
		gradleDockerVersion = '0.17.2'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://dl.bintray.com/palantir/releases" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.junit.platform:junit-platform-gradle-plugin:${junitPlatformVersion}")
		classpath("com.palantir.gradle.docker:gradle-docker:${gradleDockerVersion}")
	}
}

def getResolvedVersionOf(configuration, dependency) {
	return configuration.resolvedConfiguration.firstLevelModuleDependencies.findAll{
	it.moduleName == dependency
	}.moduleArtifacts.first()
}

configure(allprojects) {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'io.spring.dependency-management'

	group = 'org.springframework.cloud.skipper.acceptance'

	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://repo.spring.io/release" }
		maven { url "https://dl.bintray.com/palantir/releases" }
		mavenLocal()
		maven {
			url "http://repo.springsource.org/ext-private-local"
			credentials {
				username = System.getenv('ARTIFACTORY_USERNAME') ?: artifactoryUsername
				password = System.getenv('ARTIFACTORY_PASSWORD') ?: artifactoryPassword
			}
		}
	}

	compileJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	compileTestJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	dependencyManagement {
		imports {
		}
		dependencies {
			dependency "com.palantir.docker.compose:docker-compose-rule-junit4:$dockerComposeRuleJunit4Version"
			dependency "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"
			// to run junit5 in sts
			dependency "org.junit.platform:junit-platform-engine:$junitPlatformVersion"
			dependency "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
			dependency "org.junit.platform:junit-platform-runner:$junitPlatformVersion"
			dependency "org.junit.platform:junit-platform-suite-api:$junitPlatformVersion"
		}
	}
}

configure(subprojects) { subproject ->
	apply plugin: 'org.springframework.boot'

	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}
}

project('spring-cloud-skipper-acceptance-core') {
	apply plugin: 'org.junit.platform.gradle.plugin'
	dependencies {
		compile('com.palantir.docker.compose:docker-compose-rule-junit4')
		compile('org.junit.jupiter:junit-jupiter-api')
		compile('org.junit.jupiter:junit-jupiter-engine')
		compile('org.junit.jupiter:junit-jupiter-params')

		compile('org.junit.platform:junit-platform-engine')
		compile('org.junit.platform:junit-platform-launcher')
		compile('org.junit.platform:junit-platform-runner')
		compile('org.junit.platform:junit-platform-suite-api')

		testCompile('org.springframework.boot:spring-boot-starter-test')
	}
}

project('spring-cloud-skipper-acceptance-image-legacy100') {
	apply plugin: 'com.palantir.docker'

	configurations {
		legacy100
	}

	dependencies {
		legacy100("org.springframework.cloud:spring-cloud-skipper-server:${springCloudSkipper100Version}")
	}

	docker {
		name "spring-cloud-skipper-acceptance-image-legacy100"
		tags "${springCloudSkipper100VersionTag}"
		dockerfile file('src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.legacy100, "spring-cloud-skipper-server").file.absolutePath[0], 'src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.legacy100, "spring-cloud-skipper-server").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	build.dependsOn = ['docker', 'dockerTag']
}

project('spring-cloud-skipper-acceptance-image-legacy101') {
	apply plugin: 'com.palantir.docker'

	configurations {
		legacy101
	}

	dependencies {
		legacy101("org.springframework.cloud:spring-cloud-skipper-server:${springCloudSkipper101Version}")
	}

	docker {
		name "spring-cloud-skipper-acceptance-image-legacy101"
		tags "${springCloudSkipper101VersionTag}"
		dockerfile file('src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.legacy101, "spring-cloud-skipper-server").file.absolutePath[0], 'src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.legacy101, "spring-cloud-skipper-server").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	build.dependsOn = ['docker', 'dockerTag']
}


project('spring-cloud-skipper-acceptance-image-legacy102') {
	apply plugin: 'com.palantir.docker'

	configurations {
		legacy102
	}

	dependencies {
		legacy102("org.springframework.cloud:spring-cloud-skipper-server:${springCloudSkipper102Version}")
	}

	docker {
		name "spring-cloud-skipper-acceptance-image-legacy102"
		tags "${springCloudSkipper102VersionTag}"
		dockerfile file('src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.legacy102, "spring-cloud-skipper-server").file.absolutePath[0], 'src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.legacy102, "spring-cloud-skipper-server").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	build.dependsOn = ['docker', 'dockerTag']
}

project('spring-cloud-skipper-acceptance-image-drivers100') {
	apply plugin: 'com.palantir.docker'

	dependencies {
		// version need to match one from custom-apps/skipper-server-with-drivers100
		compile('org.springframework.cloud.skipper.acceptance.app:skipper-server-with-drivers100:1.0.0.BUILD-SNAPSHOT')
	}

	docker {
		name "spring-cloud-skipper-acceptance-image-drivers100"
		dockerfile file('src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.compile, "skipper-server-with-drivers100").file.absolutePath[0], 'src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.compile, "skipper-server-with-drivers100").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	build.dependsOn 'docker'
}

project('spring-cloud-skipper-acceptance-image-drivers101') {
	apply plugin: 'com.palantir.docker'

	dependencies {
		// version need to match one from custom-apps/skipper-server-with-drivers101
		compile('org.springframework.cloud.skipper.acceptance.app:skipper-server-with-drivers101:1.0.1.BUILD-SNAPSHOT')
	}

	docker {
		name "spring-cloud-skipper-acceptance-image-drivers101"
		dockerfile file('src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.compile, "skipper-server-with-drivers101").file.absolutePath[0], 'src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.compile, "skipper-server-with-drivers101").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	build.dependsOn 'docker'
}

project('spring-cloud-skipper-acceptance-image-drivers102') {
	apply plugin: 'com.palantir.docker'

	dependencies {
		// version need to match one from custom-apps/skipper-server-with-drivers102
		compile('org.springframework.cloud.skipper.acceptance.app:skipper-server-with-drivers102:1.0.2.BUILD-SNAPSHOT')
	}

	docker {
		name "spring-cloud-skipper-acceptance-image-drivers102"
		dockerfile file('src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.compile, "skipper-server-with-drivers102").file.absolutePath[0], 'src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.compile, "skipper-server-with-drivers102").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	build.dependsOn 'docker'
}

project('spring-cloud-skipper-acceptance-tests') {
	apply plugin: 'org.junit.platform.gradle.plugin'

	dependencies {
		compile project(":spring-cloud-skipper-acceptance-core")
		testCompile('org.springframework.boot:spring-boot-starter-test')
		testCompile('org.springframework.boot:spring-boot-starter-web')
	}

	build.dependsOn ':spring-cloud-skipper-acceptance-image-legacy100:docker'
	build.dependsOn ':spring-cloud-skipper-acceptance-image-legacy101:docker'
	build.dependsOn ':spring-cloud-skipper-acceptance-image-legacy102:docker'
	build.dependsOn ':spring-cloud-skipper-acceptance-image-drivers100:docker'
	build.dependsOn ':spring-cloud-skipper-acceptance-image-drivers101:docker'
	build.dependsOn ':spring-cloud-skipper-acceptance-image-drivers102:docker'
}




